import org.springframework.boot.gradle.tasks.run.BootRun

buildscript {
  ext {
    kotlinVersion = '1.3.71'
    springBootVersion = '2.2.6.RELEASE'
    junitVersion = "5.3.1"

  }
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    classpath("com.github.ben-manes:gradle-versions-plugin:0.27.0")
    classpath("com.moowork.gradle:gradle-node-plugin:1.2.0")
  }
}

plugins {
  id 'org.owasp.dependencycheck' version '5.2.4'
}

ext {
  springSessionVersion = 'Bean-SR6'
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.moowork.node'

group = 'com.boclips.lti'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

compileKotlin {
  kotlinOptions {
    freeCompilerArgs = ["-Xjsr305=strict"]
    jvmTarget = "1.8"
  }
}
compileTestKotlin {
  kotlinOptions {
    freeCompilerArgs = ["-Xjsr305=strict"]
    jvmTarget = "1.8"
  }
}

repositories {
  mavenCentral()
  maven { url "https://repo.spring.io/milestone" }
  maven { url 'https://jitpack.io' }
}

test {
  useJUnitPlatform()
}

configurations {
  developmentOnly
  runtimeClasspath {
    extendsFrom developmentOnly
  }
}

node  {
  version = '12.16.3'
  npmVersion = '6.13.4'
  download = true
}

def frontendDir = 'frontend'

npm_install {
  inputs.file file("$frontendDir/package.json")

  outputs.dir file("$frontendDir/node_modules")

  workingDir = file(frontendDir)
}

npm_run_lint {
  workingDir = file(frontendDir)
}
npm_run_lint.dependsOn npm_install

npm_test {
  workingDir = file(frontendDir)
}
npm_test.dependsOn npm_install
npm_test.dependsOn npm_run_lint
test.dependsOn npm_test

npm_run_build {
  inputs.files fileTree("$frontendDir/src")
  inputs.files fileTree("$frontendDir/public")
  inputs.file file("$frontendDir/tsconfig.json")

  workingDir = file(frontendDir)

  outputs.dir file("$frontendDir/build")
}
npm_run_build.dependsOn npm_install

task copyFrontendOutput(type: Copy) {
  from npm_run_build.outputs
  into "$buildDir/resources/main/static"
}
bootJar.dependsOn copyFrontendOutput
copyFrontendOutput.dependsOn npm_run_build

task cleanFrontendOutput(type: Delete) {
  delete "$frontendDir/build"
}
clean.dependsOn cleanFrontendOutput

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.session:spring-session-data-mongodb'
  implementation 'org.springframework.retry:spring-retry'

  implementation 'com.auth0:java-jwt:3.10.1'
  implementation 'com.auth0:jwks-rsa:0.11.0'

  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation 'org.jetbrains.kotlin:kotlin-reflect'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'

  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.1'

  implementation 'io.github.microutils:kotlin-logging:1.4.9'

  implementation 'io.sentry:sentry-logback:1.7.21'
  implementation "io.opentracing.contrib:opentracing-spring-jaeger-web-starter:2.0.3"

  implementation "org.imsglobal:basiclti-util:1.2.0"

  implementation 'com.github.boclips:boclips-spring-security:0.377.0'
  implementation 'com.github.boclips:boclips-spring-web:0.128.0'
  implementation 'com.github.boclips:videos:v15'
  implementation 'com.damnhandy:handy-uri-templates:2.1.8'
  implementation 'joda-time:joda-time:2.10.2'

  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  testImplementation('com.github.tomakehurst:wiremock-jre8:2.25.1')
  testImplementation('ru.lanwen.wiremock:wiremock-junit5:1.3.1')
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:2.23.0'
  testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.session:spring-session-bom:${springSessionVersion}"
  }
}

dependencyCheck {
  failBuildOnCVSS = 7 // Fail on high severity or greater

  data {
    directory='./dependency-check-cache'
  }

  analyzers {
    assemblyEnabled = false
  }

  suppressionFile = './dependency-check-suppressions.xml'

  outputDirectory = 'security-report'
}

task bootRunLocal(type: BootRun) {
  main 'com.boclips.lti.LtiApplicationKt'
  classpath = sourceSets.main.runtimeClasspath
  systemProperty 'spring.profiles.active', 'local'
  systemProperty 'keycloak.realm', 'boclips'
  systemProperty 'keycloak.url', 'https://login.staging-boclips.com/auth'
  systemProperty 'boclips.api.tokenUrl', 'https://api.staging-boclips.com/v1/token'
  systemProperty 'boclips.api.baseUrl', 'https://api.staging-boclips.com/v1'
  systemProperty 'boclips.frontend.ltiTokenUrl', 'http://localhost:8080/auth/token'
  systemProperty 'boclips.frontend.apiBaseUrl', 'https://api.staging-boclips.com'
  systemProperty 'boclips.dev-support.initialiseDevelopmentSession', "true"
  systemProperty 'boclips.dev-support.developmentSessionUrl', "http://localhost:8080/dev-support/initialise-session"
  systemProperty 'boclips.dev-support.integrationId', "sandbox"
}

bootRunLocal.dependsOn copyFrontendOutput
