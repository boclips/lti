import org.springframework.boot.gradle.tasks.run.BootRun

buildscript {
  ext {
    kotlinVersion = '1.4.21'
    springBootVersion = '2.3.5.RELEASE'
    junitVersion = "5.3.1"

  }
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    classpath("com.github.ben-manes:gradle-versions-plugin:0.27.0")
  }
}

plugins {
  id 'org.owasp.dependencycheck' version '6.0.3'
  id 'com.github.node-gradle.node' version '2.2.4'
}

ext {
  springSessionVersion = 'Dragonfruit-SR1'
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'com.github.ben-manes.versions'

group = 'com.boclips.lti'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

compileKotlin {
  kotlinOptions {
    freeCompilerArgs = ["-Xjsr305=strict"]
    jvmTarget = "1.8"
  }
}
compileTestKotlin {
  kotlinOptions {
    freeCompilerArgs = ["-Xjsr305=strict"]
    jvmTarget = "1.8"
  }
}

repositories {
  mavenCentral()
  maven { url "https://repo.spring.io/milestone" }
  maven { url 'https://jitpack.io' }
}

test {
  useJUnitPlatform()
}

configurations {
  developmentOnly
  runtimeClasspath {
    extendsFrom developmentOnly
  }
}

node {
  nodeModulesDir = file('frontend/')
  version = '12.16.3'
  npmVersion = '6.13.4'
  download = true
}

def frontendDir = 'frontend'

npm_ci {
  inputs.file file("$frontendDir/package.json")

  outputs.dir file("$frontendDir/node_modules")

  workingDir = file(frontendDir)
}

npm_run_lint {
  workingDir = file(frontendDir)
}
npm_run_lint.dependsOn npm_ci

npm_test {
  workingDir = file(frontendDir)
}
npm_test.dependsOn npm_ci
npm_test.dependsOn npm_run_lint
test.dependsOn npm_test

npm_run_build {
  inputs.files fileTree("$frontendDir/src")
  inputs.files fileTree("$frontendDir/public")
  inputs.file file("$frontendDir/tsconfig.json")

  workingDir = file(frontendDir)

  outputs.dir file("$frontendDir/dist")
}
npm_run_build.dependsOn npm_ci

task copyFrontendOutput(type: Copy) {
  from npm_run_build.outputs
  into "$buildDir/resources/main/static"
}
bootJar.dependsOn copyFrontendOutput
copyFrontendOutput.dependsOn npm_run_build

task cleanFrontendOutput(type: Delete) {
  delete "$frontendDir/dist"
}
clean.dependsOn cleanFrontendOutput

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.session:spring-session-data-mongodb'
  implementation 'org.springframework.retry:spring-retry'
  implementation 'org.springframework.hateoas:spring-hateoas'
  implementation 'org.springframework.boot:spring-boot-starter-validation'

  implementation 'com.auth0:java-jwt:3.11.0'
  implementation 'com.auth0:jwks-rsa:0.15.0'

  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation 'org.jetbrains.kotlin:kotlin-reflect'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2-native-mt'

  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.12.0'

  implementation 'io.github.microutils:kotlin-logging:1.4.9'

  implementation "io.micrometer:micrometer-core"
  implementation "io.micrometer:micrometer-registry-prometheus"

  implementation 'io.sentry:sentry-logback:3.2.0'
  implementation "io.opentracing.contrib:opentracing-spring-jaeger-web-starter:3.2.2"

  implementation "org.imsglobal:basiclti-util:1.2.0"

  implementation 'com.github.boclips:boclips-spring-security:0.558.0'
  implementation 'com.github.boclips:boclips-spring-web:0.453.0'
  implementation 'com.github.boclips:videos:v15'
  implementation 'io.github.openfeign:feign-core:10.7.0'
  implementation("com.github.boclips:user-service:267")
  implementation 'com.damnhandy:handy-uri-templates:2.1.8'
  implementation 'joda-time:joda-time:2.10.10'

  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  testImplementation('com.github.tomakehurst:wiremock-jre8:2.25.1')
  testImplementation('ru.lanwen.wiremock:wiremock-junit5:1.3.1')
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
  testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.session:spring-session-bom:${springSessionVersion}"
  }
}

dependencyCheck {
  failBuildOnCVSS = 7 // Fail on high severity or greater

  data {
    directory='./dependency-check-cache'
  }

  analyzers {
    assemblyEnabled = false
  }

  suppressionFile = './dependency-check-suppressions.xml'

  outputDirectory = 'security-report'
}

task bootRunLocal(type: BootRun) {
  main 'com.boclips.lti.LtiApplicationKt'
  classpath = sourceSets.main.runtimeClasspath
  systemProperty 'spring.profiles.active', 'local'
  systemProperty 'keycloak.realm', 'boclips'
  systemProperty 'keycloak.url', 'https://login.staging-boclips.com/auth'
  systemProperty 'boclips.api.tokenUrl', 'https://api.staging-boclips.com/v1/token'
  systemProperty 'boclips.api.baseUrl', 'https://api.staging-boclips.com/'
  systemProperty 'boclips.frontend.ltiBaseUrl', 'http://localhost:8081'
  systemProperty 'boclips.frontend.apiBaseUrl', 'https://api.staging-boclips.com'
  systemProperty 'boclips.lti.v1p3.signingKeys[0].generationTimestamp', '123456'
  systemProperty 'boclips.lti.v1p3.signingKeys[0].privateKey', '-----BEGIN PRIVATE KEY-----MIIEvQIBADANBgkqhkiG9w0BAQEFAASCBKcwggSjAgEAAoIBAQCsOLoT3PE/dEWHMfuJMgYD2l9Q5qB9vARkYaQwBmSK3N+Eu7IAH2tDKH75WF3+9lxq6z2goD4+N/fXeWPcV8AvZYcnogAt+YoVsCx8BibnP5XYBIUPT1ZP6EHnm7MFZ1DFC+TVJLuuYy69TKvsm+6VbRC25OTnNZmiolnyWla3yBUx1fb851Z0f4hJFrCUtEveB+FKtlsh/p5XDqzutxNdU7dulmRExHBhedVjCOqD6nNQSCfW+afBoocwtqiGg7piIqAydVpaTwoP+AI93eVEb9+K0K1syL9a/EHavqKg/2GxMkNuh4shcBTqllFIJRum3Fw2cOy/pRje9HwQJHlfAgMBAAECggEATdr7K92W5iOvBtYuqzAFDhrbOhx4cz7bEXIeomNRcoE1PrEHYAzUFbEp7ZjoLUm8L82Wt5WZbdEpZqyG22aECCkyt1UCFcaYiQNyqMKzNZlGKTip2mj4UPljScvWW1wAZIPi+AQ6IDTQPV1632UCr2R1yOijHm7ZvG7+kJQMXdoEoC3nUfoExX+hb95nRvoJkwF9xgcehZwnHyxHNhfNvxdLLprW447ir04Hp3EjS581g2QIoAIhHzhpbXjz4cEbTxKl5m8+N7/FYJ5W3+jUEc1Bq5gNyR1urHLpxBLAhfxpm1KW5EjQnGvi6+J6N3GY5Q9yf2HcsU/CZod1mlnOwQKBgQDfJyxcuF0LAPq5TqP7I3iEDQjJDT6A+rK+nJ44UmtYQkradM+z1KL1KwYpizgYuPeD4Kb+K7b+TiY27b5WDCQ7jKERNGjJE3miJaVkLPljOd/Z7s+B+lIMDTg94g8dejXlOyjjBWy9fD8g3juq+7F0lhno/yW39yk4XuWwPmvb4QKBgQDFklx2UIWjuImqSQmINpg0wArvN8iRx+ETjYMxqtRXwUYkIh6jqdyYUmGakOlPSOI/seh7yCYK5g67/uUPELa9n/oWvytDMuQNCXmih/cGe0Lj7dFW/IqPB+ZnZp4cymRS1z5Z7ltDqt9fdHjOFNdmEF4m4FR6Iec9sDs1ZMz9PwKBgQDTbR0V+EOHYnUqzzyEfFX53xOc0aSPtXdXK7KosEYuSBj9o8EKpAOFgC8Fe3c5zT4Nv/LYTHjDty1zy4sToctOdG5Ci8JgCMisfTiXkhjzD2ls5VvLzWyhKIFJ+H3Tsu+PcZ7KgWBGo4p4TLaByJiYWoLQA7Yr98whMaAzfODaYQKBgGxkXuveQ2NOE+DjjyI26YxPK/CQBy7LnRUwFyEBmtvGQmgCfBLIVykOUVQOWqHP16igP27hoNVr3/uN3TdFsSwZf4hJOz/zxJ/OafgcZg8dQtZvm+EATjgoWmSyLN6Yvy+fvSpzlYhrT388FCbj0kTBiBYGDvKudQIoFGO3uKrbAoGAcXH7YXYYf/xnE5niahnvRJR6LeL29y0+Ngu3BnfEyYB4c31v2XB4bGcFt7RAWcRZMJJN9rG8bi+oGhaEyVdG1T7mzSp1fUdJ73mnO+oypUIMAAkFOaTplZy4hhz/No6XyncCHeTFNQQ6oBBsO5K8FsncFcrqL+zEA2jBfKEiuBA=-----END PRIVATE KEY-----'
  systemProperty 'boclips.lti.v1p3.signingKeys[0].publicKey', '-----BEGIN PUBLIC KEY-----MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEArDi6E9zxP3RFhzH7iTIGA9pfUOagfbwEZGGkMAZkitzfhLuyAB9rQyh++Vhd/vZcaus9oKA+Pjf313lj3FfAL2WHJ6IALfmKFbAsfAYm5z+V2ASFD09WT+hB55uzBWdQxQvk1SS7rmMuvUyr7JvulW0QtuTk5zWZoqJZ8lpWt8gVMdX2/OdWdH+ISRawlLRL3gfhSrZbIf6eVw6s7rcTXVO3bpZkRMRwYXnVYwjqg+pzUEgn1vmnwaKHMLaohoO6YiKgMnVaWk8KD/gCPd3lRG/fitCtbMi/WvxB2r6ioP9hsTJDboeLIXAU6pZRSCUbptxcNnDsv6UY3vR8ECR5XwIDAQAB-----END PUBLIC KEY-----'
  systemProperty 'boclips.dev-support.initialiseDevelopmentSession', "true"
  systemProperty 'boclips.dev-support.developmentSessionUrl', "http://localhost:8081/dev-support/initialise-session"
  systemProperty 'boclips.dev-support.integrationId', "sandbox"
}

bootRunLocal.dependsOn copyFrontendOutput
